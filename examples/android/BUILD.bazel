load("@build_bazel_rules_android//android:rules.bzl", "android_binary", "android_library")
load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_rust//rust:defs.bzl", "rust_library")

rust_library(
    name = "rust_lib",
    srcs = ["demo.rs"],
    edition = "2018",
)

cc_library(
    name = "jni_shim",
    srcs = ["android_link_hack.c"],  # Required because of https://github.com/bazelbuild/rules_rust/issues/1271
    linkopts = [
        "-lm",  # Required to avoid dlopen runtime failures unrelated to rust
    ],
    deps = [":rust_lib"],
    alwayslink = True,  # Required since JNI symbols appear to be unused
)

android_library(
    name = "android_main",
    srcs = [
        "AndroidMain.java",
        "JniShim.java",
    ],
    custom_package = "com.example.androidapp",
    manifest = "AndroidManifest.xml",
    resource_files = ["res/layout/android_main.xml"],
    deps = [":jni_shim"],
)

# It is intentionally mandatory to include `libstd.so` as a dependency to `android_binary`
# so that when `@rules_rust//rust/setting:std_dylib` is set, all `android_binary`s
# are not defaulted to including `libstd.so` in the output apk because it will
# greatly increase the apk size.
cc_library(
    name = "libstd_so",
    srcs = [
        # TODO: Implement mechanism to filter out libtest.so
        "@rust_linux_aarch64__aarch64-linux-android__stable_tools//:rust_std-aarch64-linux-android",
    ],
)

android_binary(
    name = "android_app",
    custom_package = "com.example.androidapp",
    manifest = "AndroidManifest.xml",
    deps = [
        ":android_main",
    ] + select({
        "@rules_rust//rust/settings:std_dylib_enabled": [":libstd_so"],
        "//conditions:default": [],
    }),
)

platform(
    name = "arm64-v8a",
    constraint_values = [
        "@platforms//cpu:arm64",
        "@platforms//os:android",
    ],
)

platform(
    name = "x86_64",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:android",
    ],
)
